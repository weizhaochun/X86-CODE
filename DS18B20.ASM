	TEMPER_L	EQU	36H
	TEMPER_H	EQU	35H
	TEMPER		EQU	34H
	TEMPER_NUM	EQU	60H
	FLAG1		BIT	00H
	DQ		BIT	P3.3

	ORG	0000H
	LJMP	MAIN
	ORG	0100H
MAIN:	MOV	SP,#70H
LP1:	LCALL	GET_TEMPER
	LCALL	TEMPER_COV
	MOV	A,TEMPER
	CPL	A
	MOV	P1,A
	LCALL	DELAY
	SJMP	LP1
	
GET_TEMPER:
	SETB	DQ
BCD:	LCALL	INIT_1820
	JB	FLAG1,S22
	LJMP	BCD
S22:	LCALL	DELAY1
	MOV	A,#0CCH
	LCALL	WRITE_1820
	MOV	A,#44H
	LCALL	WRITE_1820
	LCALL	DELAY
CBA:	LCALL	INIT_1820
	JB	FLAG1,ABC
	LJMP	CBA
ABC:	LCALL	DELAY1
	MOV	A,#0CCH
	LCALL	WRITE_1820
	MOV	A,#0BEH
	LCALL	WRITE_1820
	LCALL	READ_1820
	RET

WRITE_1820:
	MOV	R2,#8
	CLR	C
WR1:	CLR	DQ
	MOV	R3,#7
	DJNZ	R3,$
	RRC	A
	MOV	DQ,C
	MOV	R3,#15H
	DJNZ	R3,$
	SETB	DQ
	NOP
	DJNZ	R2,WR1
	SETB	DQ
	RET

READ_1820:
	PUSH	02H
	PUSH	04H
	MOV	R4,#2
	MOV	R1,#36H
RE00:	MOV	R2,#8
RE01:	CLR	C
	SETB	DQ
	NOP
	CLR	DQ
	NOP
	NOP
	NOP
	NOP
	NOP
	SETB	DQ
	MOV	R3,#5
	DJNZ	R3,$
	MOV	C,DQ
	MOV	R3,#1CH
	DJNZ	R3,$
	RRC	A
	DJNZ	R2,RE01
	MOV	@R1,A
	DEC	R1
	DJNZ	R4,RE00
	POP	04H
	POP	02H
	RET
	
TEMPER_COV:
	MOV	A,#0F0H
	ANL	A,TEMPER_L
	SWAP	A
	MOV	TEMPER_NUM,A
	MOV	A,TEMPER_L
	JNB	ACC.3,TEMPER_COV1
	INC	TEMPER_NUM
TEMPER_COV1:
	MOV	A,TEMPER_H
	ANL	A,#07H
	SWAP	A
	ADD	A,TEMPER_NUM
	MOV	TEMPER_NUM,A
	MOV	TEMPER,TEMPER_NUM
	RET

INIT_1820:
	SETB	DQ
	NOP
	CLR	DQ
	MOV	R0,#0ECH
TSR1:	DJNZ	R0,TSR1
	SETB	DQ
	MOV	R0,#1CH
TSR2:	DJNZ	R0,TSR2
	JNB	DQ,TSR3
	LJMP	TSR4
TSR3:	SETB	FLAG1
	LJMP	TSR5
TSR4:	CLR	FLAG1
	LJMP	TSR7
TSR5:	MOV	R0,#0E0H
TSR6:	DJNZ	R0,TSR6
TSR7:	SETB	DQ
	RET
	
DELAY1:	MOV	R7,#20H
	DJNZ	R7,$
	RET

DELAY:	PUSH	00H
	PUSH	01H
	MOV	R0,#00H
LP:	MOV	R1,#00H
	DJNZ	R1,$
	DJNZ	R0,LP
	POP	01H
	POP	00H
	RET

	END
