	DAT	BIT	P3.2
	CLK	BIT	P3.3
	CS	BIT	P3.4
	ORG	0000H
	LJMP	0100H
	ORG	000BH
	LJMP	INT_T0
	ORG	0023H
	LJMP	INT_SBUF
	ORG	0100H
START:	
	MOV	SP,#60H
	MOV	TMOD,#21H
	MOV	TL1,#0E8H
	MOV	TH1,#0E8H
	MOV	TL0,#0FCH
	MOV	TH0,#04BH
	MOV	PCON,#00H
	MOV	SCON,#40H
	SETB	ET0
	SETB	PT0
	SETB	ES
	SETB	EA
	SETB	TR1
	SETB	TR0
	SETB	DAT
	MOV	R1,#20
	CLR	A
	LCALL	AD
	SJMP	$
INT_SBUF:
	CLR	TI
	LCALL	AD
	RETI

INT_T0:	PUSH	PSW
	MOV	TL0,#0FCH
	MOV	TH0,#04BH
	DJNZ	R1,EXIT
	MOV	R1,#20
	MOV	SBUF,A
EXIT:	POP	PSW
	RETI

AD:	MOV	R0,#00H
	MOV	20H,#00H
	MOV	21H,#00H
	
L1:	LCALL	TLC549_ADC
	ADD	A,20H
	MOV	20H,A
	MOV	A,#00H
	ADDC	A,21H
	MOV	21H,A
	LCALL	DELAY
	DJNZ	R0,L1
	MOV	A,21H
	RET


TLC549_ADC:
	PUSH	07H
	CLR	A
	SETB	CS
	CLR	CLK		;设为空闲状态
	MOV	R7,#08H		;8位数据
	CLR	CS
	NOP
	NOP
LOOP1:	SETB	CLK
	MOV	C,DAT
	RLC	A
	CLR	CLK
	DJNZ	R7,LOOP1
	SETB	CS
	CLR	CLK
	POP	07H
	RET

DELAY:	PUSH	00H
	MOV	R0,#00H
	DJNZ	R0,$
	POP	00H
	RET

	END
	
